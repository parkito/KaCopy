group 'ru.siksmfp'
version '0.0.8'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply from: rootProject.projectDir.path + '/dependencies.gradle'

jar {
    archiveName = 'KaCopy_' + version + '.jar'
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

repositories {
    mavenCentral()
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

//signing {
//    sign configurations.archives
//}

def getSonatypeProperty(String name) {
    name = 'sonatype' + name.capitalize()
    if (project.hasProperty(name)) {
        return project.getProperties()[name]
    } else {
        return ''
    }
}

String pomName = 'KaCopy'
String pomRepositoryUrl = 'https://github.com/parkito/KaCopy'
String pomScmUrl = 'https://github.com/parkito/KaCopy'
String pomDescription = 'KaCopy'
String pomLicenseName = 'MIT'
String pomLicenseUrl = 'https://github.com/parkito/KaCopy/blob/master/LICENSE'
String pomDeveloperId = 'parkito'
String pomDeveloperEmail = 'parkito@siksmfp.ru'
String pomDeveloperName = 'Artyom Karnov'
String currentGroupId = 'ru.siksmfp'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: getSonatypeProperty('username'), password: getSonatypeProperty('password'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: getSonatypeProperty('username'), password: getSonatypeProperty('password'))
            }

            pom.project {
                name pomName
                description pomDescription
                url pomRepositoryUrl
                version version
                groupId currentGroupId
                artifactId pomName
                packaging 'jar'
                scm {
                    connection pomScmUrl
                    developerConnection pomScmUrl
                    url pomScmUrl
                }
                licenses {
                    license {
                        name pomLicenseName
                        url pomLicenseUrl
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id pomDeveloperId
                        name pomDeveloperName
                        email pomDeveloperEmail
                    }
                }
            }
        }
    }
}

dependencies {
    compile "org.slf4j:slf4j-api:$self4jVersion"
    //test
    testCompile "org.junit.jupiter:junit-jupiter-api:$jUnitApiVersion"
    testCompile "org.junit.jupiter:junit-jupiter-engine:$jUnitEngineVersion"
}